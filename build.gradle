plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
    id "io.freefair.lombok" version "8.6"
}

group = "io.github.beduality"

repositories {
    mavenCentral()
    maven {
        url "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT"
    implementation "co.aikar:acf-paper:0.5.0-SNAPSHOT"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

bukkit {
    name = "BED"
    version = project.version
    main = "io.github.beduality.core.CorePlugin"
    description = "BED PaperMC plugin"
    apiVersion = "1.20"
    author = "Block-Entity Duality Team"
    website = "https://github.com/beduality/papermc-plugin"
}

shadowJar {
   archiveBaseName.set('BED')
   archiveClassifier.set('')
   archiveVersion.set(project.version)
}

task cleanPlugins() {
    delete fileTree(dir: "../server/data/plugins", include: "BED*.jar")
}

task buildmv(dependsOn: ['clean', 'shadowJar', 'cleanPlugins'], type: Copy) {
    from "${buildDir}/libs"
    into "../server/data/plugins"
}
